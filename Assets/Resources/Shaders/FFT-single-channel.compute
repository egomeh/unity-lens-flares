//--------------------------------------------------------------------------------------
// Copyright 2014 Intel Corporation
// All Rights Reserved
//
// Permission is granted to use, copy, distribute and prepare derivative works of this
// software for any purpose and without fee, provided, that the above copyright notice
// and this statement appear in all copies.  Intel makes no representations about the
// suitability of this software for any purpose.  THIS SOFTWARE IS PROVIDED "AS IS."
// INTEL SPECIFICALLY DISCLAIMS ALL WARRANTIES, EXPRESS OR IMPLIED, AND ALL LIABILITY,
// INCLUDING CONSEQUENTIAL AND OTHER INDIRECT DAMAGES, FOR THE USE OF THIS SOFTWARE,
// INCLUDING LIABILITY FOR INFRINGEMENT OF ANY PROPRIETARY RIGHTS, AND INCLUDING THE
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  Intel does not
// assume any responsibility for any errors which may appear in this software nor any
// responsibility to update it.
//--------------------------------------------------------------------------------------

#pragma kernel ButterflySLM

int _ButterflyCount;
int _RowPass;

#define Length 512

RWTexture2D<float4> Result;

Texture2D<float3> TextureSourceR  : register(t0);
Texture2D<float3> TextureSourceI  : register(t1);
RWTexture2D<float> TextureTargetR  : register(u0);
RWTexture2D<float> TextureTargetI  : register(u1);

static const float PI = 3.14159265f;

void GetButterflyValues(uint passIndex, uint x, out uint2 indices, out float2 weights)
{
    int sectionWidth = 2 << passIndex;
    int halfSectionWidth = sectionWidth / 2u;

    int sectionStartOffset = x & ~(sectionWidth - 1);
    int halfSectionOffset = x & (halfSectionWidth - 1);
    int sectionOffset = x & (sectionWidth - 1);

    sincos(2. * PI * sectionOffset / (float)sectionWidth, weights.y, weights.x);
    weights.y = -weights.y;

    indices.x = sectionStartOffset + halfSectionOffset;
    indices.y = sectionStartOffset + halfSectionOffset + halfSectionWidth;

    if (passIndex == 0)
    {
        indices = reversebits(indices) >> (32 - _ButterflyCount) & (Length - 1);
    }
}

groupshared float pingPongArray[4][Length];
void ButterflyPass(int passIndex, uint x, uint t0, uint t1, out float resultR, out float resultI)
{
    uint2 Indices;
    float2 Weights;
    GetButterflyValues(passIndex, x, Indices, Weights);

    float inputR1 = pingPongArray[t0][Indices.x];
    float inputI1 = pingPongArray[t1][Indices.x];

    float inputR2 = pingPongArray[t0][Indices.y];
    float inputI2 = pingPongArray[t1][Indices.y];

    resultR = inputR1 + Weights.x * inputR2 - Weights.y * inputI2;
    resultI = inputI1 + Weights.y * inputR2 + Weights.x * inputI2;
}

[numthreads(Length, 1, 1)]
void ButterflySLM(uint3 position : SV_DispatchThreadID)
{
    uint2 texturePos = uint2(position.xy);

    if (_RowPass == 1)
    {
        texturePos = uint2(position.xy);
    }
    else
    {
        texturePos = uint2(position.yx);
    }

    // Load entire row or column into scratch array
    pingPongArray[0][position.x].x = TextureSourceR[texturePos].x;

    if (_RowPass == 1)
    {
        pingPongArray[1][position.x].x = 0;
    }
    else
    {
        pingPongArray[1][position.x].x = TextureSourceI[texturePos].x;
    }

    uint4 textureIndices = uint4(0, 1, 2, 3);
    for (int i = 0; i < _ButterflyCount - 1; i++)
    {
        GroupMemoryBarrierWithGroupSync();
        ButterflyPass(i, position.x, textureIndices.x, textureIndices.y, pingPongArray[textureIndices.z][position.x].x, pingPongArray[textureIndices.w][position.x].x);
        textureIndices.xyzw = textureIndices.zwxy;
    }

    // Final butterfly will write directly to the target texture
    GroupMemoryBarrierWithGroupSync();

    // The final pass writes to the output UAV texture
    ButterflyPass(_ButterflyCount - 1, position.x, textureIndices.x, textureIndices.y, TextureTargetR[texturePos].x, TextureTargetI[texturePos].x);
}
